buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.0.RELEASE")
    }
}

allprojects {
    apply plugin: 'base'
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    version =  '0.1.0'
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/libs-release" }
    }
    dependencies {
        compile("org.springframework.data:spring-yarn-boot:2.0.0.RELEASE")
    }
    task copyJars(type: Copy) {
        from "$buildDir/libs"
        into "$rootDir/gs-yarn-testing-dist/target/gs-yarn-testing-dist/"
        include "**/*.jar"
    }
    assemble.doLast {copyJars.execute()}
}

project('gs-yarn-testing-client') {
    apply plugin: 'spring-boot'
}

project('gs-yarn-testing-appmaster') {
    apply plugin: 'spring-boot'
}

project('gs-yarn-testing-container') {
    apply plugin: 'spring-boot'
}

project('gs-yarn-testing-dist') {
    dependencies {
        compile project(":gs-yarn-testing-client")
        compile project(":gs-yarn-testing-appmaster")
        compile project(":gs-yarn-testing-container")
        testCompile("org.springframework.data:spring-yarn-boot-test:2.0.0.RELEASE")
        testCompile("org.hamcrest:hamcrest-core:1.2.1")
        testCompile("org.hamcrest:hamcrest-library:1.2.1")
    }
    test.dependsOn(':gs-yarn-testing-client:assemble')
    test.dependsOn(':gs-yarn-testing-appmaster:assemble')
    test.dependsOn(':gs-yarn-testing-container:assemble')
    clean.doLast {ant.delete(dir: "target")}
    jar.enabled = false
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
